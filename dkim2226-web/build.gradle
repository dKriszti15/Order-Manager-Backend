plugins {
    id 'java'
    id 'war'
}

group = 'edu.bbte.idde.dkim2226.web'
version = 'unspecified'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.0.0'

    implementation project(':dkim2226-backend')

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.15.3'

    implementation group: 'com.github.jknack', name: 'handlebars', version: '4.2.0'
}

def deploymentName = 'ROOT'

def tomcatHome = System.getenv('CATALINA_HOME')
logger.quiet("Tomcat home detected as '${tomcatHome}'")


task deploy(type: Copy, dependsOn: 'war') {

    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }

    from war.archiveFile

    rename war.archiveFileName.get(), "${deploymentName}.war"

    into "${tomcatHome}/webapps"
}


task undeploy(type: Delete) {

    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }

    delete "${tomcatHome}/webapps/${deploymentName}.war"
}

